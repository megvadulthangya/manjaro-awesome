# Maintainer: Gyöngyösi Gábor <gabor at gshoots dot hu>
# Fork: megvadulthangya/Dark-Matter-GRUB-Theme (awesome branch, nordified)

pkgname="dmgrub-nord-1080p-ai"
pkgver=r20260217.g4ee319f
pkgrel=1
pkgdesc="Dark Matter GRUB theme (Manjaro style, nordified) - 1080p AI"
arch=('any')
url="https://github.com/megvadulthangya/Dark-Matter-GRUB-Theme.git"
license=('GPL3')
makedepends=('git')

install="${pkgname}.install"

_srcname="Dark-Matter-GRUB-Theme"
source=("git+${url}#branch=awesome")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_srcname}"
  local ts hash
  ts="$(git show -s --format=%ct HEAD)"
  hash="$(git rev-parse --short=7 HEAD)"
  printf "r%s.g%s" "$(date -u -d "@${ts}" +%Y%m%d)" "${hash}"
}

package() {
  cd "${srcdir}/${_srcname}"

  local themedir="${pkgdir}/usr/share/grub/themes/${pkgname}"
  install -d -m 755 "${themedir}"

  # Background (style = manjaro, resolution = 1080p, AI)
  install -m 644 "assets/backgrounds/manjaro-1080p-AI.png" "${themedir}/background.png"

  # Icons: "color"
  cp -a "assets/icons-1080p/color" "${themedir}/icons"

  # Fonts + base theme files (theme.txt, png slices, etc.)
  cp -a "assets/fonts/1080p/." "${themedir}/"
  cp -a "base/1080p/." "${themedir}/"
}
